# Create IAM role for the Lambda function
resource "aws_iam_role" "lambda_role" {
  name = "lambda_s3_transfer_role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Principal = {
          Service = "lambda.amazonaws.com"
        }
        Effect = "Allow"
        Sid = ""
      }
    ]
  })
}

# Attach policy to the IAM role for S3 access
resource "aws_iam_role_policy" "lambda_s3_policy" {
  name = "lambda_s3_policy"
  role = aws_iam_role.lambda_role.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = [
          "s3:GetObject",
          "s3:PutObject",
          "s3:ListBucket"
        ]
        Resource = [
          aws_s3_bucket.external.arn,
          "${aws_s3_bucket.external.arn}/*",  # This allows access to objects in the external bucket
          aws_s3_bucket.internal.arn,
          "${aws_s3_bucket.internal.arn}/*"
        ]
      }
    ]
  })
}
# Lambda-to-slack IAM Role
resource "aws_iam_role" "lambda_exec_role" {
  name = "lambda_sns_to_slack_role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17",
    Statement = [{
      Action = "sts:AssumeRole",
      Effect = "Allow",
      Principal = {
        Service = "lambda.amazonaws.com"
      }
    }]
  })
}

# Attach basic Lambda execution policy
resource "aws_iam_role_policy_attachment" "lambda_execution_policy" {
  role       = aws_iam_role.lambda_exec_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSLambdaBasicExecutionRole"
}
